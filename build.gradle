plugins {
    id 'application'
    id 'java-library'
    id 'org.openjfx.javafxplugin' version '0.0.10'
}

javafx {
    version = '17'
    modules = [
        'javafx.base',
        'javafx.controls',
        'javafx.fxml',
        'javafx.swing',
        'javafx.media'
    ]
}

description = 'JME Video Player'

// select one source-code (JDK) option:
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// select one version of the Engine:
ext.jmeVersion = '3.5.2-stable' // from mavenCentral

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
    options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) { // Java runtime options:
    args = []
    classpath sourceSets.main.runtimeClasspath
    //debug true
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xbatch'
    //jvmArgs '-Xms256m', '-Xmx256m'
    //jvmArgs '-XX:+PrintCompilation'
    //jvmArgs '-XX:+UseConcMarkSweepGC'
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

repositories {
    mavenCentral()
    //maven { url 'http://nifty-gui.sourceforge.net/nifty-maven-repo' }
}

dependencies {
    // You can read more about how to add dependencies here:
    //   https://docs.gradle.org/current/userguide/dependency_management.html#sec:how_to_declare_your_dependencies

    // jMonkeyEngine
    implementation 'org.jmonkeyengine:jme3-core:' + jmeVersion
    implementation 'org.jmonkeyengine:jme3-desktop:' + jmeVersion
    implementation 'org.jmonkeyengine:jme3-effects:' + jmeVersion
    //implementation 'org.jmonkeyengine:jme3-networking:' + jmeVersion
    //implementation 'org.jmonkeyengine:jme3-niftygui:' + jmeVersion
    implementation 'org.jmonkeyengine:jme3-terrain:' + jmeVersion

    // Physics
    implementation 'com.github.stephengold:Minie:4.9.0+big3'
    
    // select one version of LWJGL
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl:' + jmeVersion  // LWJGL 2.x
    //runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jmeVersion // LWJGL 3.x

    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jmeVersion
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jmeVersion
    runtimeOnly 'org.jmonkeyengine:jme3-testdata:' + jmeVersion
    
}

// cleanup tasks
clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs')
task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
